// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlinVersion = '1.3.20'

    repositories {
        google()
        jcenter()
        mavenCentral()
        maven { url "https://jitpack.io" }
        maven {
            url 'https://maven.fabric.io/public'
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.3.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:$kotlinVersion"
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
        classpath 'com.google.gms:google-services:4.2.0'
        classpath 'io.fabric.tools:gradle:1.26.1'
    }
}

ext.mySupportVersion = "28.0.0"
ext.myLifeCycleVersion = "1.1.1"
ext.myRoomVersion = "1.1.1"
ext.myBuildToolsVersion = "28.0.2"
ext.myMinSdkVersion = 23
ext.myTargetSdkVersion = 28
ext.myCompileSdkVersion = 28
ext.pdfnetGroup = { dependencyHandler ->
    delegate = dependencyHandler

    implementation "com.pdftron:pdftron@aar"
    implementation "com.pdftron:floatingactionbutton@aar"
    implementation "com.pdftron:pagecropper@aar"
}

ext.supportLibGroup = { dependencyHandler ->
    delegate = dependencyHandler

    // Android SDK dependencies
    implementation "com.android.support:support-v4:$mySupportVersion"
    implementation "com.android.support:appcompat-v7:$mySupportVersion"
    implementation "com.android.support:recyclerview-v7:$mySupportVersion"
    implementation "com.android.support:design:$mySupportVersion"
    implementation "com.android.support.constraint:constraint-layout:1.1.2"
    implementation "android.arch.lifecycle:extensions:$myLifeCycleVersion"

    implementation "commons-io:commons-io:2.4"
    implementation "org.apache.commons:commons-lang3:3.5"
    implementation "com.google.code.gson:gson:2.8.0"

    // RxJava
    implementation "io.reactivex.rxjava2:rxjava:2.2.2"
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'
}

allprojects {
    repositories {
        google()
        jcenter()
        flatDir {
            dirs '../../../lib', '../../../lib/full'
        }
        maven { url "https://jitpack.io" }
        maven {
            url 'https://maven.google.com/'
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
